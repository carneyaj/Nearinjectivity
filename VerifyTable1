//This Magma code verifies the ramification types of the above polynomials $f(X)$ and verifies that each such $f(X)$ is indecomposable.  Since the ramification
//data implies that f isn't cyclic or dihedral, Fried's theorem implies that $F_{f,1}$ is irreducible.

//The code also determines all cases in which f is equivalent to a polynomial of the form
//$x^i g(x^m)$ with $m>1$, with result listed in Table 8.
//Finally, we verify by inspection that the listed polynomials comprise all polynomials (up to equivalence)
//having the corresponding ramification types.  We do this directly in cases 1 and 4.  In the other
//cases, $f(X)$ has only two finite branch points, so any polynomial with the prescribed ramification type
//may be translated to yield a polynomial in which 0 is a branch point with the same ramification type as
//our $f(X)$.  If one preimage of 0 has different ramification index than all others, then we may translate it
//to 0.  In case (1.16) we translate to make the cubic factor (consisting of unramified points) have
//no degree-two term, and in case (1.14) we translate to remove the degree-one term from the factor
//which gets cubed.  In each case, one of the factors consisting of all numbers of a given ramification index
//has a nonzero term of degree one less than the degree of the factor, so by scaling on the right we may
//assume that the coefficient of this term is any prescribed nonzero multiple of the leading term of the factor,
//and then by scaling on the left we may assume that the polynomial is monic.  Finally, any two distinct
//polynomials satisfying all of these constraints are inequivalent.  In every case besides (1.1) and (1.4),
//we have exhibited the same number of indecomposable polynomials satisfying these constraints as the number of
//right-equivalence classes of indecomposable polynomials with prescribed branch points and with the given ramification
//type (this number being computed via Riemann's existence theorem after Table 5.  Since all such polynomials
//have exactly two finite branch points, and each of our polynomials has zero as a branch point, by scaling
//on the left we may make the second branch point be any desired value.  Hence we have exhibited all polynomials
//having each of the ramification types in Table 5 for which $F_{f,1}$ is irreducible of genus 0 or 1.

// Magma

// Case 1.1

K<a>:=FunctionField(Rationals());
R<x>:=PolynomialRing(K);
f:=x^2*(x^2+x+a);
bppol:=x^2 + (1/2*a^2 - 9/16*a + 27/256)*x + 1/16*a^4 - 1/64*a^3;
Discriminant(bppol) eq -1/2 * (a-9/32)^3;
Evaluate(bppol,0) eq a^3*(a-1/4)/16;
// hence bppol has two distinct nonzero roots when a isn't 0, 1/4 or 9/32
L:=ext<K|bppol>;
f:=ChangeRing(R,L)!f;  bppol:=ChangeRing(R,L)!bppol; x:=ChangeRing(R,L)!x;
for c in Roots(x*bppol) do
	{* [Degree(i[1]),i[2]]: i in Factorization(f-c[1]) *} eq {* [1,2],[2,1] *};
end for;
// For each value of a other than 0, 1/4, and 9/32, we have exhibited three distinct numbers c for which f-c has a multiple root.  Each such number c must be the image under f of a root of the derivative of f.  But this derivative has degree 3, so it must be squarefree and hence f-c has factorization type 1,1,2 for each of our three numbers c.  Note that before checking the degree of the derivative, it was conceivable that there could have been a value of a for which one of these polynomials f-c had factorization type 2,2 or 1,3 or 4.

// Next check that the polynomial is indecomposable.  Since it's monic of degree 4 and has no constant term, it can only decompose as the composition of two degree 2 polynomials, which we may assume are monic with no constant term.  Moreover, equating coefficients of x^3 shows that the polyomial on the right must be  x^2+x/2.  Then equating coefficients of x^2 shows that the polynomial on the left must be  x^2+(a-1/4)x.  The composition of these two polynomials equals f if and only if  a=1/4,  which has already been excluded.  Hence f is indecomposable.  Now Fried's theorem implies that F_{f,1} is irreducible (since f isn't cyclic or dihedral), so its genus is given by our genus formula, and we check that this genus is 1:

&+[&+[ i-GCD(i,j): i,j in {* k[2]^^Degree(k[1]): k in Factorization(f-c[1]) *}]: c in Roots(x*bppol)] eq 2*Degree(f)-2;

// Next determine for which a's is f equivalent to a polynomial of the form  X^i g(X^m)  with m>1.
Evaluate(f,x-1/4) eq x^4 + (a - 3/8)*x^2 + (-1/2*a + 1/8)*x + 1/16*a - 3/256; 
//Since this polynomial has no term of degree 3, if f is equivalent to a polynomial of the presribed form then such a polynomial must be obtained from the above polynomial by composing on the left with a degree-one polynomial and on the right with a polynomial of the form cx.  In particular, since this polynomial has a term of degree 1 (since a isn't 1/4), and also a term of degree 4, the only possibility is that i=1 and m=3, so that a=3/8.

// Next show that any degree-4 polynomial with three distinct finite branch points is equivalent to x^2*(x^2+x+a)  for some  a  which isn't 0, 1/4 or 9/32.  If we have a degree-4 polynomial with three finite branch points, then translating on both sides moves one of the branch points and its ramified preimage to 0; scaling on the right makes the coefficient of x^3 be either 0 or equal to the coefficient of x^4; and scaling on the left makes the polynomial monic.  Then the polynomial is either  x^2*(x^2+a)  or  x^2*(x^2+x+a), and our above computation showed that x^2*(x^2+x+a) has three finite branch points if a isn't 0, 1/4 or 9/32. One easily checks that for the excluded a-values the polynomials do not have three finite branch points:
R<x>:=PolynomialRing(Rationals());
 _<y>:=PolynomialRing(R);
for a in {0,1/4,9/32} do f:=y*(y^2+y+a); 
	&+[Degree(i[1]):i in Factorization(Resultant(Derivative(f),f-x))] lt 3; 
end for;
// Finally, note that x^2*(x^2+a) is a function of x^2, and so it cannot have three finite branch points (since its derivative has at most three roots, two of which are the square roots of -a, which have the same image under the polynomial).


// Case 1.4

K<a>:=FunctionField(Rationals());
R<x>:=PolynomialRing(K);
f:=x*(x^2+a*x+1)^2;
bppol:=x^2 + (108/3125*a^5 - 36/125*a^3 + 16/25*a)*x + 16/3125*a^4 - 128/3125*a^2 + 256/3125;
Discriminant(bppol) eq 2^4*3^6/5^10*(a^2-5)^2*(a^2-20/9)^3;
Evaluate(bppol,0) eq 2^4/5^5*(a^2-4)^2;
// hence bppol has two distinct nonzero roots when a^2 isn't 4, 5 or 20/9
L:=ext<K|bppol>;
f:=ChangeRing(R,L)!f;  bppol:=ChangeRing(R,L)!bppol;
{* [Degree(i[1]),i[2]]: i in Factorization(f) *} eq {* [1,1],[2,2] *};
for c in Roots(bppol) do
	{* [Degree(i[1]),i[2]]: i in Factorization(f-c[1]) *} eq {* [1,2],[3,1] *};
end for;
// For each value of a with a^2 not 4, 5 or 20/9, we have exhibited three distinct numbers c for which f-c has a multiple root, where one of these values (namely c=0) yields f-c with at most one simple root.  Since the derivative of f has degree 4, the only possibility is that two of these values c have a unique ramified preimage which has ramification index 2, and the third value c has precisely two ramified preimages both of which have ramification index 2.

// Since the polynomial has prime degree, it is indecomposable; its ramification implies that it is not cyclic or dihedral, so Fried's theorem implies that F_{f,1} is irreducible, and we check that its genus is 1:

&+[&+[ i-GCD(i,j): i,j in {* k[2]^^Degree(k[1]): k in Factorization(f-c[1]) *}]: c in Roots(x*bppol)] eq 2*Degree(f)-2;

// Next determine for which a's is f equivalent to a polynomial of the form  X^i g(X^m)  with m>1.
x:=Parent(f).1;
f:=Evaluate(f,x-2*a/5);
f-Evaluate(f,0) eq x^5 + (2-3/5*a^2)*x^3 + 2/25*a*(a^2-5)*x^2 + (6*a^2/25-1)*(2*a^2/5-1)*x;
// Since this polynomial has no term of degree 4, if f is equivalent to a polynomial of the prescribed form then such a polynomial must be obtained from the above polynomial by composing on both sides with polynomials of the form cx (with c a nonzero constant).  Since a^2 isn't 5, this polynomial has a term of degree 2 unless a=0 (which is a solution with i=1 and m=2).  So suppose there is a term of degree 2; then there cannot be terms of degree either 1 or 3, but that is impossible.

// Next show that every polynomial with this ramification type is equivalent to one of the above polynomials. By translating the [1,2^2] point and its unramified preimage to 0, we may assume that the polynomial is cx(x^2+ax+b)^2 where b,c aren't 0.  By scaling on both sides, we may assume that b=c=1, so that the polynomial is  x(x^2+ax+1)^2,  which we treated above when  a^2  isn't 4, 5 or 20/9.  By scaling on both sides, we can turn this into  x(x^2+x+1/a^2)^2, which we now treat:
for asquared in [4,5,20/9] do
	R<x>:=PolynomialRing(Rationals()); 
	_<y>:=PolynomialRing(R);
	f:=y*(y^2+y+1/asquared)^2; 
	&+[Degree(i[1]):i in Factorization(Resultant(Derivative(f),f-x))] eq 2;
end for;



// All remaining cases of Table 1

P2<t,a>:=PolynomialRing(Rationals(),2);
S:=[
< t^2*(t^2+t+1),a,1,{*{*1^^2,2*}^^3*}>,//case (1.1) when a=1
< t^3*(t^2+15*t+60),a,1,{*{*1^^2,3*}^^2*} >,// case (1.2)
< t^3*(t^2+5*t+40),a,0,{*{*1,2^^2*},{*1^^2,3*}*} >,// case (1.3)
< t*(t^2+t+1)^2,a,1,{*{*1,2^^2*},{*1^^3,2*}^^2*} >,// case (1.4) when a=1
< t^4*(t^2-6*t+25),a,1,{*{*1^^2,4*},{*1^^2,2^^2*}*} >,// case (1.5)
< t*(t-1)^2*(t-2/3*(a^2+a+2))^3,a^3-2,1,{*{*1,2,3*},{*1^^2,2^^2*}*} >,// case (1.6)
< t*(t-1)^2*(t-(a+2)/4)^4,a^2+a+2,1,{*{*1,2,4*},{*1^^3,2^^2*}*} >,// case (1.7), first half
< t*(t-1)^2*(t-(a+2)/4)^4,a^2-13*a-5,1,{*{*1,2,4*},{*1^^3,2^^2*}*} >,// case (1.7), second half
< t^4*(t^3+7*t^2+56*t+700),a,1,{*{*1,2^^3*},{*1^^3,4*}*} >,// case (1.8)
< t*(t^2+7/2*t+7)^3,a,1,{*{*1,3^^2*},{*1^^4,3*}*} >,// case (1.9)
< t*(t^2+t+(a+11)/56)^3,a^2+7,0,{*{*1,3^^2*},{*1^^3,2^^2*}*} >,// case (1.10)
< t^3*(t^2+7*t+(3*a+17)*7/2)^2,a^2-21,1,{*{*2^^2,3*},{*1^^3,2^^2*}*} >,// case (1.11)
< t^3*(t-75/2)^2*(t^2+1/6*(76*a^2 + 56*a - 89)*t + 32*a^2 + 592*a - 298),a^3-7/2,1,
{*{*1^^2,2,3*},{*1,2^^3*}*} >,// case (1.12)
< t^2*(t^2+4*t+(a+1)/2+3)^3,a^2+1,1,{*{*2,3^^2*},{*1^^4,2^^2*}*} >,// case (1.13)
< (t^2+22*a+25)^3*(t^2-8*t+54*a+97),a^2-2,1,{*{*1^^2,3^^2*},{*1^^2,2^^3*}*} >,// case (1.14)
< t*(t^2+6*t-a^2+a+7)^4,a^3-3,1,{*{*1,4^^2*},{*1^^5,2^^2*}*} >,// case (1.15)
< (t^2-3*t+(5*a-9)/2)^3*(t^3+3/2*(7*a-9)*t+3/2*(29*a-17)),a^2+3,1,
{*{*1^^3,3^^2*},{*1,2^^4*}*} >,// case (1.16)
< t*(t^3+30*t^2+15*(a^4+2*a^3+a^2+2*a+19)*t+10*(14*a^4+25*a^3+8*a^2+22*a+74))^3,a^5-2,1,
{*{*1,3^^3*},{*1^^4,2^^3*}*} >// case (1.17)
];

for s in S do F:=s[1]; apol:=s[2];
	if not IsIrreducible(apol) then "WRONG!"; end if;
	if Degree(apol) gt 1 then K<c>:=NumberField(UnivariatePolynomial(apol));
	else K:=Rationals(); c:=0; end if;
	R<x>:=PolynomialRing(K);
	f:=Evaluate(ChangeRing(P2,K)!F,[x,c]);
// first compute the ramification type of f over a few points (which will turn out to be the full branch locus)
	_<y>:=PolynomialRing(R);
	fy:=Evaluate(f,y);
	bppol:=Resultant(Derivative(fy),fy-x);
	rams:={* *};
	for fac in Factorization(bppol) do fpol:=fac[1];
		if Degree(fpol) gt 1 then L<d>:=ext<K|fpol>; else L:=K; d:=Roots(fpol)[1,1]; end if;
		rams join:={* {* i[2]^^Degree(i[1]): i in Factorization(ChangeRing(Parent(f),L)!f-d) *}^^Degree(fpol) *};
end for;
// next check that f is indecomposable
f:=f/LeadingCoefficient(f);  
n:=Degree(f);
for m in {i:i in Divisors(n)} diff {1,n} do gdeg:=n div m;  g:=x^gdeg;  h:=x^m;
	for i in [1..m-1] do h+:=x^(m-i)*Coefficient(f-h^gdeg,n-i)/gdeg;  end for;
		while f ne Evaluate(g,h) do term:=LeadingTerm(f-Evaluate(g,h));
			if IsDivisibleBy(Degree(term),m) then g+:=LeadingCoefficient(term)*x^(Degree(term) div m);
			else continue m; end if; 
		end while;
		"f decomposes as the composition of these polynomials",g,h;
	end for;
// next check that f isn't equivalent to a polynomial of the form x^i g(x^m) with m>1
	f:=Evaluate(f,x-Coefficient(f,n-1)/n);  f-:=Evaluate(f,0);  r:=GCD({Degree(i):i in Terms(f)});
	if r gt 1 then "polynomial is Ritt twistable",f,r; end if;
// finally check that the ramification we computed matches the ramification type listed in Table 3, and that it accounts for all ramification of f,  and that the genus of F_{f,1} is as claimed
	<rams eq s[4],
 	&+[&+[i-1:i in j]: j in rams] eq Degree(f)-1,
	 s[3] eq -Degree(f)+2+1/2*&+[&+[i-GCD(i,j):i,j in k]: k in rams]>;
end for;
